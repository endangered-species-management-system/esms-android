plugins {
    id 'com.android.application'
}

apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion 31

    //noinspection GroovyAssignabilityCheck
    defaultConfig {
        applicationId "edu.cnm.deepdive.esms"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        String propertyFile = "$projectDir/../../../services/esms.properties"

        buildConfigField 'String', 'BASE_URL', quoted(getProperty(propertyFile, 'base_url'))
        buildConfigField 'String', 'CLIENT_ID', quoted(getProperty(propertyFile, 'client_id'))
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    //noinspection GroovyAssignabilityCheck
    buildFeatures {
    //noinspection GroovyAssignabilityCheck
        viewBinding true
    }
}

dependencies {

    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Basic Android support libraries
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Material Design components
    implementation 'com.google.android.material:material:1.5.0'

    // Navigation libraries
    implementation 'androidx.fragment:fragment:1.4.1'
    implementation "androidx.navigation:navigation-fragment:$navigation_version"
    implementation "androidx.navigation:navigation-ui:$navigation_version"

    // Preferences/settings library.
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Lifecycle (LiveData and ViewModel) libraries
    def lifecycleVersion = '2.4.1'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    // Room libraries & connectors
    def roomVersion = '2.4.2'
    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"

    // Retrofit dependencies
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    // ReactiveX libraries
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    // Gson (JSON parser) library
    implementation 'com.google.code.gson:gson:2.9.0'

    // Google Sign-in library.
    implementation 'com.google.android.gms:play-services-auth:20.1.0'

    // Picasso library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // CSV Library
    implementation 'org.apache.commons:commons-csv:1.9.0'

    // Stetho (inspection) dependency
    implementation 'com.facebook.stetho:stetho:1.6.0'

    // Testing libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

static String getProperty(String filename, String propname) {
    Properties props = new Properties()
    props.load(new FileInputStream(filename))
    return props.getProperty(propname)
}

static String quoted(String input) {
    return "\"$input\""
}